// --------------------------------- //
// Project: JumpIt3D
// Start: Tuesday, April 16, 2013
// IDE Version: 10.283

// 2,5D Jump'n Run
?IF 0
// - Spielfigur - Kugel
 - Gegner
 	* ...
 - Trampolin  - ...
 - Hindernis - ...
 - Aufzug - ...
 - Level - Anzahl ...
 - Speichermöglichkeit
 - Dekoration & Themes
 - Spezialelement (Knobel)
 - KEIN Mapeditor (WIRKLICH!)
 - Wasser
 - Geschichte
 - Plattform
?ENDIF
SETCURRENTDIR("Media")
SYSTEMPOINTER TRUE

CONSTANT GOD_MODE = TRUE


CONSTANT GAMESTATE_MENU = 0
CONSTANT GAMESTATE_GAME = 1
CONSTANT GAMESTATE_OPTION = 2

GLOBAL GameState% = GAMESTATE_GAME

GLOBAL CamX, CamY, CamZ, CamToX, CamToY, CamToZ
CONSTANT PLAYER_2_CAMERADIST = 18
CamX = PLAYER_2_CAMERADIST
CamY = 0
CamZ = 0


GOSUB Init
WHILE TRUE
	GOSUB Update
	GOSUB Render

	SHOWSCREEN
WEND

SUB Init:
	SELECT GameState
		CASE GAMESTATE_GAME
			// init modules
			GOSUB Init_Player
			GOSUB Init_Trampoline

			// game stuff
			CurrentMap.Init("map/map0.ddd", "map/map0.data")

			CurrentPlayer.Init(0, 0)
			
			X_MAKE3D 1, 500, 45
			X_MAKE2D
		CASE GAMESTATE_MENU
			// menu stuff

		CASE GAMESTATE_OPTION
			// option stuff

		DEFAULT
			DEBUG "Unknown Gamestate"
	ENDSELECT
ENDSUB

SUB Update:
	SELECT GameState
		CASE GAMESTATE_GAME
			// game stuff
			CurrentMap.Update()
			CurrentPlayer.Update()
		CASE GAMESTATE_MENU
			// menu stuff

		CASE GAMESTATE_OPTION
			// option stuff

		DEFAULT
			DEBUG "Unknown Gamestate"
	ENDSELECT
ENDSUB

SUB Render:
	SELECT GameState
		CASE GAMESTATE_GAME
			// game stuff
			X_MAKE3D 1, 500, 45
			X_CAMERA CamX, CamY, CamZ, CamToX, CamToY, CamToZ
			X_SPOT_LT 0, RGB(255,255,255), CamX, CamY, CamZ, CamToX, CamToY, CamToZ, 360
			//X_AMBIENT_LT 0, RGB(220,220,220)
			X_MOVEMENT 0,0,0
			X_DRAWAXES 0,0,0

			// 3d stuff
			CurrentMap.Render()

			CurrentPlayer.Render()



			// debug
			IF GOD_MODE
				LOCAL mx, my, ml, mr
				LOCAL ox1, oy1, oz1, ox2, oy2, oz2
				LOCAL dirx, diry, dirz
				LOCAL posx, posy, posz
				MOUSESTATE mx, my, ml, mr


				X_SCREEN2WORLD mx, my, 0, ox1, oy1, oz1
				X_SCREEN2WORLD mx, my, 1000, ox2, oy2, oz2

				dirx = ox1 - ox2
				diry = oy1 - oy2
				dirz = oz1 - oz2

				LOCAL dist = X_COLLISIONRAY(CurrentMap.ObjID, 0, ox1, oy1, oz1, dirx, diry, dirz)

				IF dist > 0
					X_PUSHMATRIX
					// normalize vector
					LOCAL veclen = SQR(dirx*dirx + diry*diry + dirz*dirz)
					dirx = dirx/veclen
					diry = diry/veclen
					dirz = dirz/veclen

					posx = ox1 + dirx*dist
					posy = oy1 + diry*dist
					posz = oz1 + dirz*dist

					X_MOVEMENT 0,0,0
					X_DOT posx, posy, posz, 32, RGB(255,255,0)
					X_POPMATRIX
				ENDIF
				X_MAKE2D

				PRINT "PlayerX: "+CurrentPlayer.M.X+ " PlayerY: "+CurrentPlayer.M.Y, 10, 10
				PRINT "PickX: "+posx+" PickY: "+posy+" PickZ "+posz, 10, 30
			ELSE
				X_MAKE2D

				// ui
			ENDIF

			// 2d stuff

		CASE GAMESTATE_MENU
			// menu stuff

		CASE GAMESTATE_OPTION
			// option stuff

		DEFAULT
			DEBUG "Unknown Gamestate"
	ENDSELECT
ENDSUB








