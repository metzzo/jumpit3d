// --------------------------------- //
// Project: JumpIt3D
// Start: Tuesday, April 16, 2013
// IDE Version: 10.283

GLOBAL TerrainWidth = 8

TYPE TMapBackSide
	F1[]
	F2[]

	FUNCTION Check%: other AS TMapBackSide
		FOR i = 0 TO 2
			IF NOT ( Equ(other.F2[i], self.F1[i]) AND Equ(other.F1[i], self.F2[i]) )
				RETURN TRUE
			ENDIF
		NEXT
		RETURN FALSE
	ENDFUNCTION
ENDTYPE

FUNCTION Equ: A, B
	LOCAL d = ABS(A-B)
	RETURN d < .2
ENDFUNCTION

TYPE TMap
	ObjID%
	ColObjID%
	OrigObjID%
	MinY%; MaxY
	

	FUNCTION Update:

	ENDFUNCTION

	FUNCTION Render:
		X_PUSHMATRIX
		X_DRAWOBJ self.ObjID, 0
		X_POPMATRIX
	ENDFUNCTION

	FUNCTION Init: Path$, InfoPath$
		self.OrigObjID = GENX_OBJ()
		X_LOADOBJ Path$, self.OrigObjID

		self.ObjID = GENX_OBJ()
		X_OBJSTART self.ObjID
		X_CULLMODE 0
		LOCAL Faces[] AS TMapBackSide
		FOR i = 0 TO X_NUMFACES(self.OrigObjID)-1
			LOCAL f[]
			X_GETFACE self.OrigObjID, 0, i, f[]
			BuildBackSide(Faces[], f[])
		NEXT

		FOR i = 0 TO X_NUMFACES(self.OrigObjID)-1
			LOCAL f[]
			X_GETFACE self.OrigObjID, 0, i, f[]
			FOR j = 0 TO 2
				X_OBJADDVERTEX f[j][0]-TerrainWidth/2, f[j][1], f[j][2], f[j][3], f[j][4], f[j][5]
				
				LOCAL myY = f[j][2]
				IF myY < self.MinY THEN self.MinY = myY
				IF myY > self.MinY THEN self.MaxY = myY
			NEXT
			X_OBJNEWGROUP

			self.CreateBackside(Faces, i)
		NEXT
		X_OBJEND
		DEBUG "MinY: "+self.MinY+" MaxY "+self.MaxY+"\n"

		self.ColObjID = GENX_OBJ()
		X_OBJSTART self.ColObjID
		FOR i = 0 TO X_NUMFACES(self.OrigObjID)-1
			self.CreateBackside(Faces, i)
		NEXT
		X_OBJEND

	ENDFUNCTION

	FUNCTION CreateBackside: Faces[] AS TMapBackSide, i%
		LOCAL f[]
		X_GETFACE self.OrigObjID, 0, i, f[]
		LOCAL TmpFace[] AS TMapBackSide
		BuildBackSide(TmpFace[], f[])

		FOR j = 0 TO 2
			LOCAL count% = 0
			FOREACH F IN Faces[]
				IF NOT F.Check(TmpFace[j])
					INC count
				ENDIF
			NEXT
			DEBUG count+"\n"
			IF count = 0
				X_OBJADDVERTEX f[j][0]+TerrainWidth/2			, f[j][1]		, f[j][2]			, f[j][3]			, f[j][4]			, RGB(255,0,0)
				X_OBJADDVERTEX f[MOD(j+1,3)][0]+TerrainWidth/2	, f[MOD(j+1,3)][1]	, f[MOD(j+1,3)][2]	, f[MOD(j+1,3)][3]	, f[MOD(j+1,3)][4]	, RGB(255,0,0)
				X_OBJADDVERTEX f[j][0]-TerrainWidth/2			, f[j][1]						, f[j][2]			, f[j][3]			, f[j][4]			, RGB(255,0,0)
				X_OBJADDVERTEX f[MOD(j+1,3)][0]-TerrainWidth/2	, f[MOD(j+1,3)][1]				, f[MOD(j+1,3)][2]	, f[MOD(j+1,3)][3]	, f[MOD(j+1,3)][4]	, RGB(255,0,0)




				X_OBJNEWGROUP
			ENDIF
		NEXT
	ENDFUNCTION
ENDTYPE

FUNCTION BuildBackSide: Faces[] AS TMapBackSide, f[]
	FOR j = 0 TO 2
		LOCAL MapSide AS TMapBackSide
		DIMDATA MapSide.F1[], f[j][0], f[j][1], f[j][2]
		DIMDATA MapSide.F2[], f[MOD(j+1,3)][0], f[MOD(j+1,3)][1], f[MOD(j+1,3)][2]
		DIMPUSH Faces[], MapSide
	NEXT
ENDFUNCTION



